(declare-const x String)
(declare-const y String)
(declare-const r (RegEx String))
(declare-const s (RegEx String))
(declare-const p (Array (_ BitVec 8) Bool)) ; predicate on characters

(echo "===== string in regex checks =====")

(echo "=== using empty string ===")

(echo "= atomic =")
; none, all, all single characters, character range, epsilon, predicate
(simplify (not (str.in.re "" re.empty)))
(simplify (not (str.in.re "" re.nostr)))
(simplify (str.in.re "" re.all))
(simplify (not (str.in.re "" re.allchar)))
(simplify (not (str.in.re "" (re.range "a" "a"))))
(simplify (not (str.in.re "" (re.range x x))))
(simplify (str.in.re "" (str.to.re "")))
(simplify (not (str.in.re "" (str.to.re "a"))))
(simplify (not (str.in.re "" (re.of.pred p))))

(echo "= standard constructors =")
; option, union, concat, star, plus
(simplify (str.in.re "" (re.opt r)))
(simplify (str.in.re "" (re.opt (str.to.re "a"))))
(simplify (str.in.re "" (re.opt (str.to.re ""))))
(simplify (str.in.re "" (re.union r (str.to.re ""))))
(simplify (not (str.in.re "" (re.union (str.to.re "a") (str.to.re "b")))))
(simplify (str.in.re "" (re.++ re.all (str.to.re ""))))
(simplify (str.in.re "" (re.++ (str.to.re "") (re.opt r))))
(simplify (not (str.in.re "" (re.++ (str.to.re "") re.allchar))))
(simplify (not (str.in.re "" (re.++ re.all re.empty))))
(simplify (not (str.in.re "" (re.++ (str.to.re "a") (str.to.re "")))))
(simplify (str.in.re "" (re.* r)))
(simplify (str.in.re "" (re.* (str.to.re "a"))))
(simplify (str.in.re "" (re.* (str.to.re ""))))
(simplify (str.in.re "" (re.* (re.of.pred p))))
(simplify (str.in.re "" (re.+ re.all)))
(simplify (not (str.in.re "" (re.+ (str.to.re "a")))))
(simplify (str.in.re "" (re.+ (str.to.re ""))))

(echo "= additional constructors =")
; additional constructors: complement, intersection
(simplify (str.in.re "" (re.complement re.allchar)))
(simplify (str.in.re "" (re.complement re.empty)))
(simplify (str.in.re "" (re.complement (str.to.re "a"))))
(simplify (str.in.re "" (re.complement (re.+ (str.to.re "a")))))
(simplify (not (str.in.re "" (re.complement re.all))))
(simplify (not (str.in.re "" (re.complement (str.to.re "")))))
(simplify (not (str.in.re "" (re.complement (re.opt r)))))
(simplify (str.in.re "" (re.inter re.all (str.to.re ""))))
(simplify (str.in.re "" (re.inter (str.to.re "") (str.to.re ""))))
(simplify (str.in.re "" (re.inter (str.to.re "") (re.opt r))))
(simplify (not (str.in.re "" (re.inter (str.to.re "") re.allchar))))
(simplify (not (str.in.re "" (re.inter re.all (str.to.re "a")))))
(simplify (not (str.in.re "" (re.inter re.empty (str.to.re "")))))
(simplify (not (str.in.re "" (re.inter (str.to.re "a") (str.to.re "b")))))

(echo "= looping (* with counting) =")
; loop types: iterate between m and n times; iterate at least m times
(simplify (str.in.re "" ((_ re.loop 0 10) r)))
(simplify (str.in.re "" ((_ re.loop 0) r)))
(simplify (str.in.re "" ((_ re.loop 0) re.all)))
(simplify (str.in.re "" ((_ re.loop 1 3) (re.opt r))))
(simplify (str.in.re "" ((_ re.loop 2) re.all)))
(simplify (not (str.in.re "" ((_ re.loop 1 3) (str.to.re "a")))))
(simplify (not (str.in.re "" ((_ re.loop 1 0) (str.to.re "b")))))
(simplify (not (str.in.re "" ((_ re.loop 2) re.allchar))))
(simplify (not (str.in.re "" ((_ re.loop 3) (str.to.re "a")))))

(echo "= variations (not currently supported) =")
; empty string
(simplify (str.in.re "" re.emptystr))
(simplify (str.in.re "" (re.diff (str.to.re "") re.allchar)))
(simplify (str.in.re "" (re.diff (re.opt r) re.empty)))
; difference
(simplify (not (str.in.re "" (re.diff r r))))
(simplify (not (str.in.re "" (re.diff (str.to.re "") re.all))))
(simplify (not (str.in.re "" (re.diff (str.to.re "a") (str.to.re "a") ))))
; looping a fixed number of times
(simplify (str.in.re "" ((_ re.^ 0) r)))
(simplify (str.in.re "" ((_ re.^ 0) re.allchar)))
(simplify (str.in.re "" ((_ re.^ 1) (str.to.re ""))))
(simplify (not (str.in.re "" ((_ re.^ 1) re.allchar))))

(echo "= complex combinations =")
(echo "TODO")

(echo "=== using nonempty string ===")

(echo "= atomic =")
; none, all, all single characters, character range, epsilon, predicate
(simplify (not (str.in.re "a" re.empty)))
(simplify (not (str.in.re "aba" re.empty)))
(simplify (not (str.in.re "a" re.nostr)))
(simplify (str.in.re "a" re.all))
(simplify (str.in.re "aba" re.all))
(simplify (str.in.re "a" re.allchar))
(simplify (str.in.re "b" re.allchar))
(simplify (not (str.in.re "aba" re.allchar)))
(simplify (str.in.re "a" (re.range "a" "b")))
(simplify (str.in.re "b" (re.range "a" "b")))
(simplify (str.in.re "c" (re.range "c" "c")))
(simplify (not (str.in.re "c" (re.range "c" "b"))))
(simplify (not (str.in.re "c" (re.range "a" "b"))))
(simplify (not (str.in.re "aba" (re.range "a" "b"))))
(simplify (str.in.re "a" (str.to.re "a")))
(simplify (not (str.in.re "a" (str.to.re ""))))
(simplify (not (str.in.re "aba" (str.to.re "a"))))
(simplify (not (str.in.re "a" (str.to.re "b"))))
(simplify (not (str.in.re "aba" (str.to.re "ab"))))
(simplify (not (str.in.re "aba" (re.of.pred p))))
(echo "TODO: checks with nontrivial predicates")

(echo "= standard constructors =")
; option, union, concat, star, plus
(simplify (str.in.re "aba" (re.opt (str.to.re "aba"))))
(simplify (not (str.in.re "a" (re.opt (str.to.re "ab")))))
(simplify (not (str.in.re "a" (re.opt (str.to.re "b")))))
(simplify (str.in.re "a" (re.union r (str.to.re "a"))))
(simplify (not (str.in.re "ab" (re.union (str.to.re "a") (str.to.re "b")))))
(simplify (str.in.re "aba" (re.++ re.all (str.to.re "a"))))
(simplify (str.in.re "aba" (re.++ (str.to.re "aba") re.all)))
(simplify (str.in.re "aba" (re.++ re.allchar (str.to.re "ba"))))
(simplify (str.in.re "a" (re.++ (str.to.re "a") (re.opt r))))
(simplify (not (str.in.re "ba" (re.++ (str.to.re "") re.allchar))))
(simplify (not (str.in.re "a" (re.++ re.all re.empty))))
(simplify (not (str.in.re "b" (re.++ (str.to.re "a") (str.to.re "")))))
(simplify (str.in.re "aaa" (re.* (str.to.re "a"))))
(simplify (not (str.in.re "aba" (re.* (str.to.re "ab")))))
(simplify (str.in.re "aba" (re.* (str.to.re "aba"))))
(simplify (str.in.re "aba" (re.* re.allchar)))
(simplify (str.in.re "a" (re.+ re.all)))
(simplify (str.in.re "aaaaaa" (re.+ (str.to.re "aa"))))

(echo "= additional constructors =")
; additional constructors: complement, intersection
(simplify (str.in.re "aba" (re.complement re.allchar)))
(simplify (str.in.re "b" (re.complement re.empty)))
(simplify (str.in.re "a" (re.complement (str.to.re ""))))
(simplify (str.in.re "a" (re.complement (str.to.re "aba"))))
(simplify (str.in.re "aba" (re.complement (re.+ (str.to.re "a")))))
(simplify (not (str.in.re "a" (re.complement re.all))))
(simplify (not (str.in.re "b" (re.complement re.allchar))))
(simplify (not (str.in.re "a" (re.complement (str.to.re "a")))))
(simplify (str.in.re "a" (re.inter re.all (str.to.re "a"))))
(simplify (str.in.re "ab" (re.inter (str.to.re "ab") (str.to.re "ab"))))
(simplify (not (str.in.re "a" (re.inter (str.to.re "") r))))
(simplify (not (str.in.re "a" (re.inter (str.to.re "") re.allchar))))
(simplify (not (str.in.re "aba" (re.inter re.all (str.to.re "a")))))
(simplify (not (str.in.re "ab" (re.inter re.empty (str.to.re "ab")))))
(simplify (not (str.in.re "c" (re.inter (str.to.re "a") (str.to.re "b")))))

(echo "= looping (* with counting) =")
; loop types: iterate n times; iterate between m and n times; iterate at least m times
(simplify (str.in.re "a" ((_ re.loop 0) re.all)))
(simplify (str.in.re "aba" ((_ re.loop 3) re.all)))
(simplify (str.in.re "aa" ((_ re.loop 1 3) (str.to.re "a"))))
(simplify (str.in.re "bb" ((_ re.loop 0 2) (str.to.re "b"))))
(simplify (str.in.re "abab" ((_ re.loop 2 4) (str.to.re "ab"))))
(simplify (str.in.re "aa" ((_ re.loop 0) (str.to.re "aa"))))
(simplify (str.in.re "aa" ((_ re.loop 2) (str.to.re "a"))))
(simplify (str.in.re "aba" ((_ re.loop 3 3) re.allchar)))
(simplify (not (str.in.re "a" ((_ re.loop 0 0) r))))
(simplify (not (str.in.re "ab" ((_ re.loop 1 3) (str.to.re "a")))))
(simplify (not (str.in.re "b" ((_ re.loop 1 0) (str.to.re "b")))))
(simplify (not (str.in.re "aaa" ((_ re.loop 1 2) (str.to.re "a")))))
(simplify (not (str.in.re "aa" ((_ re.loop 3) re.allchar))))

(echo "= complex combinations =")
(simplify (str.in.re "aaaaaaa" (re.+ (re.union (str.to.re "aa") (str.to.re "aaa")))))
(simplify (not (str.in.re "aaaaaaa" (re.+ (re.union (str.to.re "aaa") (str.to.re "aaaaa"))))))
(echo "TODO")

(echo "=== using variable strings ===")
(echo "TODO")
